import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

configurations.kotlinCompilerClasspath { configuration ->
    def dep = "org.jetbrains.kotlin:kotlin-compiler-embeddable:1.3.70-dev-withExperimentalGoogleExtensions-20200424"
    withDependencies { it -> it.add(project.dependencies.create(dep))}
        .resolutionStrategy
        .force(dep)
}

dependencies {
    kotlinCompilerPluginClasspath "androidx.compose:compose-compiler:0.1.0-dev14"
}

project.tasks.withType(KotlinCompile.class).configureEach { compile ->
    compile.kotlinOptions.useIR = true
}

kotlin {
    jvm()

    sourceSets {
        commonMain {
            kotlin.srcDirs += file("$aosp_location/frameworks/support/compose/compose-runtime/src/commonMain/kotlin")
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.7"
            }
        }
        jvmMain {
            kotlin.srcDirs += file("$aosp_location/frameworks/support/compose/compose-runtime/src/jvmMain/kotlin")
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
                implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3"
            }
        }
    }
}
